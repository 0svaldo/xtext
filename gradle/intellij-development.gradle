/*
 * Cross-cutting configuration for all IntelliJ projects
 */

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath "org.xtext:xtext-idea-gradle-plugin:0.3.25"
	}
}

apply plugin: org.xtext.gradle.idea.IdeaDevelopmentPlugin

ideaDevelopment {
	ideaVersion = "143.249.6"
	pluginDependencies {
		id 'junit'
	}
}

tasks.withType(Test) {
	systemProperty 'idea.system.path', "${project.buildDir}/idea-test-system" //TODO should be part of the idea-dev plugin
	systemProperty 'idea.config.path', "${project.buildDir}/idea-test-config" //TODO should be part of the idea-dev plugin
	minHeapSize = "1024m"
	jvmArgs '-XX:MaxPermSize=256m', '-ea'
	/*
	 * We have an active annotation that allows us to delegate
	 * to existing tests and run them in an IntelliJ context.
	 * The Delegates themselves are not valid JUnit tests, but
	 * look like one. So we need to filter them out here
	 */
	exclude {it.name.endsWith('Delegate.class')}
}

tasks.withType(org.xtext.gradle.idea.tasks.RunIdea) {
	jvmArgs '-ea' //TODO should be part of the idea-dev plugin
}

plugins.withType(org.xtext.gradle.idea.IdeaComponentPlugin) {
	dependencies {
		ideaProvided files(((URLClassLoader)javax.tools.ToolProvider.getSystemToolClassLoader()).getURLs()).filter({it.name == "tools.jar"})
	}
}