plugins {
	id "com.terrafolio.jenkins" version "1.2.3"
}

ext.jdks = [
	'1.8' : 'JDK 1.8 Autoinstall',
	'1.7' : 'JDK 1.7 Autoinstall',
	'1.6' : 'JDK 1.6 Autoinstall'
]

ext.branches = [
	[
		name: 'master',
		displayName: 'master',
		platforms: [
			/* FIXME oldest: [
				target: 'Helios',
				jdk: jdks['1.6']
			],*/
			latest: [
				target: 'Mars',
				jdk: jdks['1.7']
			],
		]
	],
	[
		name: 'maintenance',
		displayName: 'maintenance',
		platforms: [
			/* FIXME oldest: [
				target: 'Helios',
				jdk: jdks['1.6']
			],*/
			latest: [
				target: 'Mars',
				jdk: jdks['1.7']
			],
		]
	]
]

jenkins {
	servers {
		typefox {
			url 'http://192.168.99.100:8080'
			secure false
			if (project.hasProperty('jenkinsUserName')) {
				username jenkinsUserName
			}
			if (project.hasProperty('jenkinsPassword')) {
				password jenkinsPassword
			}
		}
	}
	
	defaultServer servers.typefox
	
	project.file('templates').listFiles().each {templateFile ->
		templates.create(templateFile.name.split('\\.')[0]) {
			xml file(templateFile)
		}
	}
}

apply from: 'pipelines.gradle'
apply from: 'verbatim-jobs.gradle'