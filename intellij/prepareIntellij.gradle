allprojects {
  ext.intellijHome = file("${rootDir}/intellij-ce")
  def osName = System.getProperty('os.name')
  ext.isWindows = osName.startsWith('Win')
  ext.isMac = osName.startsWith('Mac')
  ext.isUnix = osName.startsWith('Linux')
  if (isWindows || isUnix) {
    ext.intellijLibs = file("${intellijHome}/lib")
  } else if (isMac) {
    ext.intellijLibs = file("${intellijHome}/IntelliJ IDEA 14 CE EAP.app/Contents/lib")
  }
}
def buildQualifier = '138.SNAPSHOT'
def archiveName
if (isWindows) {
  archiveName = "ideaIC-${buildQualifier}.win.zip"
} else if (isMac) {
  archiveName = "ideaIC-${buildQualifier}.mac.zip"
} else if (isUnix) {
  archiveName = "ideaIC-${buildQualifier}.tar.gz"
}
def archiveFile = file("${intellijHome}/${archiveName}")

task downloadIntellij {
  onlyIf {!archiveFile.exists()}
  doLast {
    intellijHome.mkdirs()
    ant.get(src:"https://teamcity.jetbrains.com/repository/download/bt410/153679:id/${archiveName}?guest", dest: intellijHome)
    ant.get(src:'https://teamcity.jetbrains.com/repository/download/bt410/153679:id/sources.zip?guest', dest: intellijHome)
  }
}

task unpackIntellij(type: Copy, dependsOn: downloadIntellij) {
  if (isUnix) {
    from tarTree(archiveFile)
    into(intellijHome)
    eachFile {file ->
      file.path = (file.path - "idea-IC-${buildQualifier}/")
    }
  } else {
    from zipTree(archiveFile)
    into intellijHome
  }
}

task prepareIntellij(dependsOn: [unpackIntellij, ':intellij-dependencies:eclipseClasspath'])

task cleanTestSystem(type: Delete) {
  delete "${intellijHome}/test-system"
}

task cleanIntellij(type: Delete) {
  delete intellijHome
}