module org.eclipse.xtext.xtend.tests.GenerateTestLanguages

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import de.itemis.xtext.antlr.XtextAntlrGeneratorFragment

var projectName = "org.eclipse.xtext.xtend.tests"
var runtimeProject = "../${projectName}"

Workflow {
	bean = StandaloneSetup {
		platformUri = "${runtimeProject}/.."
	} 
	
	component = DirectoryCleaner {
		directory="${runtimeProject}/src-gen"
	} 
	
	component = Generator {
		pathRtProject = "${runtimeProject}"
		pathUiProject = "${runtimeProject}"
		projectNameRt = "${projectName}"
		activator = "org.eclipse.xtext.xtend.XtendTestsActivator"
		language = {
			uri="classpath:/org/eclipse/xtext/xtend/TreeTestLanguage.xtext"
			fragment = ecore.EcoreGeneratorFragment {}
			fragment = grammarAccess.GrammarAccessFragment {}
			fragment = parseTreeConstructor.ParseTreeConstructorFragment {}
			fragment = resourceFactory.ResourceFactoryFragment {}
			fragment = validation.CheckFragment {}
			fragment = XtextAntlrGeneratorFragment {
				options = {
					classSplitting = true
				}
			}
		}
	}
}
