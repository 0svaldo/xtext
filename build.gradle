allprojects {
	ext.isBuildServer = System.getenv("JOB_NAME") != null || project.hasProperty('buildServer')
	buildscript {
		repositories {
			if (!isBuildServer) {
				mavenLocal()
			}
			jcenter()
			maven {
				url "https://oss.sonatype.org/content/repositories/snapshots/"
			}
		}
	}

	group = 'org.eclipse.xtext'
	version = '2.9.0-SNAPSHOT'

	repositories.addAll(rootProject.buildscript.repositories)

	apply from: "$rootDir/gradle/eclipse-settings.gradle"

	plugins.withType(JavaPlugin) {
		project.apply from: "$rootDir/gradle/unroll-dependencies.gradle"
		sourceCompatibility = 1.6
		tasks.withType(JavaCompile) {
			options.encoding = 'ISO-8859-1'
		}
		tasks.withType(Test) {
			if (isBuildServer) {
				ignoreFailures = true
			}
		}
	}
}
