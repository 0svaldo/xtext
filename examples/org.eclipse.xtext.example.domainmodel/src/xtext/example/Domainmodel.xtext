/*******************************************************************************
 * Copyright (c) 2009 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.example.Domainmodel

generate domainmodel "http://www.example.xtext/Domainmodel"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

File :
  (imports+=Import)*
  (namedElements+=NamedElement)*;
  
Import :
  'import' importURI=STRING;
  
NamedElement :
	Package | Type;
	
Package :
  'package' name=QualifiedName '{'
    (namedElements+=NamedElement)*
  '}';
	
Type :
  Entity | DataType;
  
DataType :
  'datatype' name=ID;
  
Entity :
  'entity' name=ID ('extends' superType=[Entity])? '{'
    (features+=Feature)*	
  '}';
  
Feature :
  StructuralFeature | Operation;
  
StructuralFeature :
  Attribute | Reference;
  
Attribute :
  'attr' name=ID ':' type=TypeRef;
  
Reference :
  'ref' name=ID ':' type=TypeRef ('opposite' opposite=[Reference])?;
  
Operation :
  'op' name=ID'('(params+=Parameter (',' params+=Parameter)*)?')' ':' type=TypeRef;
  
Parameter :
  name=ID type=TypeRef;
  
TypedElement :
  Feature | Parameter;
  
TypeRef :
	referenced=[Type];
	
QualifiedName returns ecore::EString : ID ('.' ID)*;
